load("@rules_proto//proto:defs.bzl", "proto_descriptor_set", "proto_library")
load("@rules_go//go:def.bzl", "go_library")
load("@rules_go//proto:def.bzl", "go_proto_library")

package(default_visibility = ["//visibility:public"])

# rules_proto (-> proto descriptor set):
#  Building a proto_library creates a file named xxxxx-descriptor-set.proto.bin,
#  which is the descriptor set's binary file.  One use case for the descriptor
#  set is generating code without having to parse .proto files.
proto_library(
    name = "hello_world_proto",
    srcs = ["hello_world.proto"],
    deps = [
        "@com_google_protobuf//:timestamp_proto",
        "@googleapis//google/api:annotations_proto",
    ],
)

# rules_go (-> *.a and *.pb.go):
# This rule takes a proto descriptor set, generated by proto_library rule, and produces xxx.a and yyy.pb.go files.
#   *.a is a compiled package, which is created by go tool pack.
#   *.pg.go contains gRPC server and client stubs.
go_proto_library(
    name = "hello_world_go_proto",
    compilers = ["@rules_go//proto:go_grpc"],
    importpath = "github.com/abitofhelp/apis/proto/hello_world",
    proto = ":hello_world_proto",
    deps = [
        "@org_golang_google_genproto_googleapis_api//annotations",
    ],
)

# rules_go:
# Generally, a single proto file is each package.
# This rule is a wrapper around the go_proto_library.
# The package can contain more *.go files, and then they will be listed in srcs parameter of the rule.
go_library(
    name = "hello_world",
    embed = [":hello_world_go_proto"],
    importpath = "github.com/abitofhelp/apis/proto/hello_world",
)

alias(
    name = "go_default_library",
    actual = ":hello_world",
    visibility = ["//visibility:public"],
)

# rules_proto:
# Generates binary descriptor sets from a proto_library.
# Although similar, a proto_descriptor_set and a proto_library have different inputs.
#   A proto_library generates descriptor_sets for FILES listed in SRCS only.
#   A proto_descriptor_set generates descriptor_sets for LABELS listed in DEPS.
#     The descriptor set includes all proto files and their dependencies.
#proto_descriptor_set(
#    name = "hello_world_descriptor_set",
#    deps = [
#        ":hello_world_proto",
#        "@com_google_protobuf//:timestamp_proto",
#        "@googleapis//google/api:annotations_proto",
#    ],
#)
